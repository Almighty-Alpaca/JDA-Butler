plugins {
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'com.github.johnrengelman.shadow' version '4.0.2' apply false
}

allprojects {
    def build = System.getenv("BUILD_NUMBER") ?: System.getProperty("BUILD_NUMBER") ?: "DEV"
    ext {
        buildNumber = build == "DEV" ? build : "#" + build
    }

    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                if (['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier -> selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/ }) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}


subprojects {
    apply plugin: 'application'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'com.github.johnrengelman.shadow'
    
    sourceCompatibility = targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'

    shadowJar { task ->
        baseName = task.project.name.capitalize()
    }

    repositories {
        jcenter()
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.25'
        compile 'ch.qos.logback:logback-classic:1.2.3'
    }
}

project(':bot') {
    tasks.run.enabled = false
}