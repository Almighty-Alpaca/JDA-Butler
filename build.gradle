plugins {
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'com.github.johnrengelman.shadow' version '6.0.0' apply false
}

allprojects {
    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                if (['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier -> selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/ }) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}


subprojects {
    apply plugin: 'application'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'com.github.johnrengelman.shadow'
    
    sourceCompatibility = targetCompatibility = 11
    compileJava.options.encoding = 'UTF-8'

    shadowJar { task ->
        archiveBaseName = task.project.name.capitalize()
    }

    repositories {
        jcenter()
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api:1.7.25'
        implementation 'ch.qos.logback:logback-classic:1.2.3'
    }
}

project(':launcher') {
    tasks.run.enabled = false
}