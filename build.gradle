plugins {
	id 'java'
	id 'application'
	id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'idea'
    id 'eclipse'
	id 'com.github.ben-manes.versions' version '0.13.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'

mainClassName = "com.almightyalpaca.discord.jdabutler.Bot"

repositories {
	mavenCentral()
	jcenter()
    maven {
        name "Fab's kindly provided JDA3 Alpha maven repo"
        url "http://nexus.notfab.net/content/repositories/JDA3"
    }
}

dependencies {
	// JDA
	compile(group: 'net.dv8tion', name: 'jda', version: '3.0.ALPHA_61') {
		// I don't want all the audio libs currectly. They are just a waste of space.
		// This breaks URLPlayer and FilePlayer but JDA-Player will still work.
		exclude group: 'junit', module: 'junit'
		exclude group: 'jflac', module: 'jflac'
		exclude group: 'net.sourceforge.jaadec', module: 'jaad'
		exclude group: 'org.tritonus', module: 'tritonus-dsp'
		exclude group: 'com.googlecode.soundlibs', module: 'tritonus-share'
		exclude group: 'com.googlecode.soundlibs', module: 'mp3spi'
	}
	
	// JOrbis
	// Also not, same reason
	// compile group: 'com.googlecode.soundlibs', name: 'jorbis', version: '0.0.17-3'

	// Updated version of jda's dependencies
	compile group: 'org.json', name: 'json', version: '20160810'
	compile group: 'net.java.dev.jna', name: 'jna', version: '4.2.2'
	compile group: 'com.neovisionaries', name: 'nv-websocket-client', version: '1.30'
	compile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'

	// Gson
	compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'

	// Commons-IO
	compile group: 'commons-io', name: 'commons-io', version: '2.5'

	// Javaparser
	compile group: 'com.github.javaparser', name: 'javaparser-core', version: '2.5.1'

	// Reflections
	compile group: 'org.reflections', name: 'reflections', version: '0.9.10'

	// Possible local jar files
	compile fileTree(dir: 'lib', include: '*.jar')

	// JUnit for testing
	testCompile group: 'junit', name: 'junit', version: '4.12'

}

idea.module {
	downloadJavadoc = true
	downloadSources = true
}

eclipse.classpath {
    downloadJavadoc = true
    downloadSources = true
}

shadowJar {
  mergeServiceFiles()
}

task wrapper(type: Wrapper) { // This task is to specify the gradle version
	gradleVersion = '3.1'
}
